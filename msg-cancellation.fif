#!/bin/env -S fift -s

"TonUtil.fif" include
"lib.fif" include

$# 0 = {
	."fift -s " $0 type ." <private key file> <comment>" cr
	."  create new cancellation request with <comment> signed by private key stored in <private key file>" cr
	."  contract data cell has to be downloaded from blockchain first" cr
	cr
	."files:" cr
	."  contract-address.addr - contract address input" cr
	."  contract-data.boc - contract data cell input" cr
	."  msg-cancellation.boc - external message output" cr

	100 halt
} if

$1 load-keypair nip
constant private_key

$2
constant comment

"contract-address.addr" load-address
2constant contract_address

"contract-data.boc" file>B B>boc
constant contract_data

now 60 +
constant valid_until

<b
	comment $len {
		<b comment 36 append-long-string b> ref,
	} if
b> constant cancellation

<b
	contract_data seqno 32 u,
	valid_until 32 u,
	cancellation ref,
b> constant payload

<b
	b{100} s,
	contract_address addr,
	payload <s s,
b> hashu constant signed_hash

signed_hash private_key ed25519_sign_uint
constant signature

<b
	signature B,
	payload <s s,
b> constant body

<b
	b{1000100} s,
	contract_address addr,
	0 Gram,
	b{00} s,
	body <s s,
b> constant msg_cancellation

msg_cancellation 2 boc+>B dup "msg-cancellation.boc" B>file
