#!/bin/env -S fift -s

"TonUtil.fif" include
"../lib.fif" include

$# 7 < {
	."fift -s " $0 type ." <contract address> <data boc file> <key file> <destination address> <amount> <comment> <msg boc file>" cr
	."  create new simple transfer request to send <amount> with <comment> to <destination address>" cr
	."  signed by private key stored in <key file>" cr
	."  message is saved to <msg boc file>" cr
	cr
	."contract data cell has to be downloaded first:" cr
	."  lite-client -c 'last' -c 'saveaccountdata <data boc file> <contract address>'" cr

	100 halt
} if

$1 false parse-load-address drop
2constant contract_address

$2 file>B B>boc
constant contract_data

$3 load-keypair nip
constant private_key

$4 false parse-load-address
constant bounceable
2constant destination_address

$5 $>GR
constant amount

$6
constant comment

$7
constant msg_boc_file

<b
	b{01} s,
	bounceable 1 i, b{000} s,
	b{100} s, destination_address addr,
	amount Gram,
	0 106 u,
	comment simple-transfer-body body,
b> constant out_msg

<b
	contract_data seqno@data 32 u,
	now 60 + 32 u,
	out_msg ref,
b> constant payload

<b
	b{100} s,
	contract_address addr,
	payload <s s,
b> hashu constant signed_hash

signed_hash private_key ed25519_sign_uint
constant signature

<b
	b{1000100} s,
	contract_address addr,
	0 Gram,
	b{00} s,
	signature B,
	payload <s s,
b> constant msg_simple_transfer

msg_simple_transfer 2 boc+>B msg_boc_file B>file
