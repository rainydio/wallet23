#!/bin/env -S fift -s

"TonUtil.fif" include
"../lib.fif" include

$# 5 < {
	."fift -s " $0 type ." <contract address> <data boc file> <key file> <new key file> <msg boc file>" cr
	."  create new request to replace public key stored in <key file> with one stored in <new key file>" cr
	."  signed by private key stored in <key file>" cr
	."  message is saved to <msg boc file>" cr
	cr
	."contract data cell has to be downloaded first:" cr
	."  lite-client -c 'last' -c 'saveaccountdata <data boc file> <contract address>'" cr

	100 halt
} if

$1 false parse-load-address drop
2constant contract_address

$2 file>B B>boc
constant contract_data

$3 load-keypair
constant private_key
constant old_public_key

$4 load-generate-keypair drop
constant new_public_key

$5
constant msg_boc_file

<b
	b{01} s,
	false 1 i, b{000} s,
	b{100} s, contract_address addr,
	1 Gram* 4 / Gram,
	0 106 u,
	<b 1 32 u, old_public_key B, new_public_key B, b> body,
b> constant out_replace_key_msg

<b
	contract_data seqno@data 32 u,
	now 60 + 32 u,
	out_replace_key_msg ref,
b> constant payload

<b
	b{100} s,
	contract_address addr,
	payload <s s,
b> hashu constant signed_hash

signed_hash private_key ed25519_sign_uint
constant signature

<b
	b{1000100} s,
	contract_address addr,
	0 Gram,
	b{00} s,
	signature B,
	payload <s s,
b> constant msg_replace_key

msg_replace_key 2 boc+>B msg_boc_file B>file
